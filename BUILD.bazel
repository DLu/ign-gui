load(
    "//ign_bazel:build_defs.bzl",
    "IGNITION_FEATURES",
    "IGNITION_ROOT",
    "IGNITION_VISIBILITY",
    "cmake_configure_file",
    "generate_include_header",
    "generate_yaml",
    "ign_config_header",
    "ign_export_header",
    "qt_cc_library",
    "qt_resource",
)

load(
    ":ign_gui_plugin.bzl",
    "ign_gui_plugin"
)

package(
    default_visibility = IGNITION_VISIBILITY,
    features = IGNITION_FEATURES,
)

licenses(["notice"])

exports_files(["LICENSE"])

PROJECT_NAME = "ignition-gui"

PROJECT_MAJOR = 4

PROJECT_MINOR = 0

PROJECT_PATCH = 0

# Generates config.hh based on the version numbers in CMake code.
ign_config_header(
    name = "config",
    src = "include/ignition/gui/config.hh.in",
    cmakelists = ["CMakeLists.txt"],
    extra_defines = [
        "CMAKE_INSTALL_PREFIX=.",
        "IGN_LIB_INSTALL_DIR=.",
    ],
    project_name = PROJECT_NAME,
    project_version = (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),
)

# Generates test_config.hh based on the version numbers in CMake code.
ign_config_header(
    name = "test_config",
    src = "test/test_config.h.in",
    cmakelists = ["CMakeLists.txt"],
    extra_defines = [
        "PROJECT_SOURCE_DIR=.",
        "CMAKE_BINARY_DIR=.",
    ],
    project_name = PROJECT_NAME,
    project_version = (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),
)

ign_export_header(
    name = "include/ignition/gui/Export.hh",
    export_base = "IGNITION_GUI",
    lib_name = "ignition-gui",
    visibility = ["//visibility:private"],
)

qt_resource(
    name="resources",
    qrc_file= "include/ignition/gui/resources.qrc",
    files=glob([
      "include/ignition/gui/qtquickcontrols2.conf",
      "include/ignition/gui/qml/*.qml",
      "include/ignition/gui/qml/images/*.png"
    ]),
)

qt_cc_library(
    name="ign_gui",
    srcs=[
      "src/Application.cc",
      "src/Conversions.cc",
      "src/Dialog.cc",
      "src/DragDropModel.cc",
      "src/Helpers.cc",
      "src/MainWindow.cc",
      "src/Plugin.cc",
      "src/PlottingInterface.cc",
      "src/SearchModel.cc",
    ],
    hdrs=[
        "include/ignition/gui/Application.hh",
        "include/ignition/gui/Dialog.hh",
        "include/ignition/gui/MainWindow.hh",
        "include/ignition/gui/Plugin.hh",
        "include/ignition/gui/PlottingInterface.hh",
    ],
    normal_hdrs=[
        "include/ignition/gui/config.hh",
        "include/ignition/gui/Export.hh",
        "include/ignition/gui/Conversions.hh",
        "include/ignition/gui/DragDropModel.hh",
        "include/ignition/gui/Enums.hh",
        "include/ignition/gui/GuiEvents.hh",
        "include/ignition/gui/Helpers.hh",
        "include/ignition/gui/qt.h",
        "include/ignition/gui/SearchModel.hh",
    ],
    includes=["include"],
    deps=[
        "@qt//:qt_core",
        "@qt//:qt_gui",
        "@qt//:qt_qml",
        "@qt//:qt_quick",
        "@qt//:qt_quick_control",
        "@qt//:qt_widgets",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_common/events",
        IGNITION_ROOT + "ign_plugin/loader",
        IGNITION_ROOT + "ign_transport",
    ]
)

cc_binary(
    name = "libignition-gui.so",
    srcs = ["src/ign.cc", "include/ignition/gui/ign.hh"],
    includes = ["include"],
    linkstatic = False,
    linkshared = True,
    deps = [
        ":ign_gui"
    ]
)

ign_gui_plugin(
    folder_name="grid_3d",
    plugin_name="Grid3D",
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_plugin/register",
        IGNITION_ROOT + "ign_rendering",
    ],
)
    
ign_gui_plugin(
    folder_name="image_display",
    plugin_name="ImageDisplay",
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_common/graphics",
        IGNITION_ROOT + "ign_plugin/register",
    ],
)

ign_gui_plugin(
    folder_name="key_publisher",
    plugin_name="KeyPublisher",
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_msgs",
        IGNITION_ROOT + "ign_plugin/register",
    ],
)

ign_gui_plugin(
    folder_name="plotting",
    plugin_name="TransportPlotting",
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_plugin/register",
    ],
)

ign_gui_plugin(
    folder_name="publisher",
    plugin_name="Publisher",
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_plugin/register",
    ],
    test_srcs = [
        "src/plugins/publisher/Publisher_TEST.cc", 
        "test/test_config.h"
    ],
    test_deps = [
        ":resources",
    ],
)

ign_gui_plugin(
    folder_name="scene3d",
    plugin_name="Scene3D",
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_common/graphics",
        IGNITION_ROOT + "ign_plugin/register",
        IGNITION_ROOT + "ign_rendering",
    ],
)

ign_gui_plugin(
    folder_name="screenshot",
    plugin_name="Screenshot",
    qrc_resources = glob([
        "src/plugins/screenshot/*.qml",
        "src/plugins/screenshot/*.png"
    ]),
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_plugin/register",
        IGNITION_ROOT + "ign_rendering"
    ],
    test_srcs = ["src/plugins/screenshot/Screenshot_TEST.cc", "test/test_config.h"],
    test_deps = [
        ":resources",
    ],
)

ign_gui_plugin(
    folder_name="topic_echo",
    plugin_name="TopicEcho",
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_plugin/register",
    ],
)

ign_gui_plugin(
    folder_name="topic_viewer",
    plugin_name="TopicViewer",
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_plugin/register",
    ],
    qrc_resources = glob([
        "src/plugins/topic_viewer/*.qml",
        "src/plugins/topic_viewer/*.png",
        "src/plugins/topic_viewer/*.svg"
    ]),
)

ign_gui_plugin(
    folder_name="world_control",
    plugin_name="WorldControl",
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_plugin/register",
    ],
)

ign_gui_plugin(
    folder_name="world_stats",
    plugin_name="WorldStats",
    deps = [
        ":ign_gui",
        IGNITION_ROOT + "ign_common",
        IGNITION_ROOT + "ign_plugin/register",
    ],
)

all_plugins = [
  ":libGrid3D.so", 
  ":libImageDisplay.so", 
  ":libKeyPublisher.so", 
  ":libTransportPlotting.so", 
  ":libPublisher.so",
  ":libScene3D.so",
  ":libScreenshot.so",
  ":libTopicEcho.so",
  ":libTopicViewer.so",
  ":libWorldControl.so",
  ":libWorldStats.so",
]

all_resources = [
    ":resources", 
    ":grid_3d_resources",
    ":image_display_resources",
    ":key_publisher_resources",
    ":plotting_resources",
    ":publisher_resources",
    ":scene3d_resources",
    ":screenshot_resources",
    ":topic_echo_resources",
    ":topic_viewer_resources",
    ":world_control_resources",
    ":world_stats_resources",
]

cc_library(
    name = "ign_gui_resources",
    deps = all_resources,
    data = all_plugins
)

cc_test(
    name = "load_plugins_TEST",
    srcs = ["src/plugins/load_plugins_TEST.cc", "test/test_config.h"],
    includes = ["test"],
    deps = [
        ":ign_gui",
        ":ign_gui_resources",
        IGNITION_ROOT + "ign_common",
        "@gtest",
        "@gtest//:gtest_main",
    ],
)

qt_resource(
    name="test_resources",
    qrc_file= "test/plugins/TestPlugin.qrc",
    files=["test/plugins/TestPlugin.qml"],
)

qt_cc_library(
    name="test_plugin",
    srcs=["test/plugins/TestPlugin.cc"],
    hdrs=["test/plugins/TestPlugin.hh"],
    deps=[
        ":ign_gui",
        IGNITION_ROOT + "ign_plugin/register",
    ]
)

cc_binary(
    name="libTestPlugin.so",
    srcs=[
        ":test_plugin",
    ],
    linkshared=True
)

qt_cc_library(
    name="test_bad_inheritance_plugin",
    srcs=["test/plugins/TestBadInheritancePlugin.cc"],
    hdrs=["test/plugins/TestBadInheritancePlugin.hh"],
    deps=[
        ":ign_gui",
        IGNITION_ROOT + "ign_plugin/register",
    ]
)

cc_binary(
    name="libTestBadInheritancePlugin.so",
    srcs=[":test_bad_inheritance_plugin"],
    linkshared=True
)

qt_cc_library(
    name="test_not_registered_plugin",
    srcs=["test/plugins/TestNotRegisteredPlugin.cc"],
    hdrs=["test/plugins/TestNotRegisteredPlugin.hh"],
    deps=[
        ":ign_gui",
        IGNITION_ROOT + "ign_plugin/register",
    ]
)

cc_binary(
    name="libTestNotRegisteredPlugin.so",
    srcs=[":test_not_registered_plugin"],
    linkshared=True
)

test_data=[
    "test/config", 
    ":libTestPlugin.so",
    ":libTestBadInheritancePlugin.so",
    ":libTestNotRegisteredPlugin.so",
]

test_deps=[
    "@gtest",
    "@gtest//:gtest_main",
    ":ign_gui",
    ":resources",
    ":test_resources",
]

test_srcs = glob(
    ["src/*_TEST.cc"],
    exclude = ["src/PluginLoader_TEST.cc"],
)

[cc_test(
    name = src.replace("/", "_").replace(".cc", "").replace("src_", ""),
    srcs = [src, "test/test_config.h"],
    includes=["test"],
    data = test_data,
    deps= test_deps,
) for src in test_srcs]

cmake_configure_file(
    name = "gui.rb",
    src = "src/cmd/cmdgui.rb.in",
    out = "cmdgui.rb",
    cmakelists = ["CMakeLists.txt"],
    defines = [
        "library_location=libignition-gui.so",
        "PROJECT_VERSION_FULL=%d.%d.%d" % (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),  # noqa
        "IGN_LIBRARY_NAME=%s" % PROJECT_NAME,
    ],
)

CMDS = "    - gui   : Launch graphical interfaces."

generate_yaml(
    name = "gui",
    commands = CMDS,
    library_name = PROJECT_NAME,
    library_version = "%d.%d.%d" % (PROJECT_MAJOR, PROJECT_MINOR, PROJECT_PATCH),
    ruby_target = "gui.rb",
)
